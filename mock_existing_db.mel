use cicd/runners::CicdDispatchEngine
use cicd/runners::CicdRunnerEngine
use cicd/runners::setupRunner
use cicd/runners::stopRunner
use cicd/steps::stepOn
use cicd/steps::stepOnWithInput
use cicd/services::setServiceState
use fs/local::readLocal
use log/logger::Logger
use log/log::logError
use log/log::logErrors
use process/command::|raw_commands
use process/environment::Environment
use process/environment::|environment
use std/data/string_map::StringMap
use std/data/string_map::|entry
use std/ops/option::|wrap
use work/resources::|container
use work/resources::|mount
use work/resources::|volume
use work/resources::|service_container
use work/resources/arch::|amd64

treatment connectAndDump[logger: Logger](
    const api_token: string,
    const location: string,
    var example_sql_file: string,
    var output_directory: string,
    // github_* are for Github Status reporting
    var github_status: bool = false,
    var github_project: string = "",
    var github_sha: string = "",
    var github_token: string = "",
    // gitlab_* are for GitLab Status reporting
    var gitlab_status: bool = false,
    var gitlab_pipeline_id: string = "",
    var gitlab_project_id: string = "",
    var gitlab_ref: string = "",
    var gitlab_root_url: string = "",
    var gitlab_sha: string = "",
    var gitlab_token: string = ""
)
  // Dispatcher defines what is in charge of spawning and reporting runners
  model dispatcher: CicdDispatchEngine(api_token=api_token, location="compose")
  // Runner represents the running entity, that can be setup and stopped
  model runner: CicdRunnerEngine()
  input trigger: Block<void>
  output dump: Stream<byte>
{
    setupRunner[dispatcher=dispatcher, runner=runner, logger=logger](
        name="db_mock",
        cpu=100,
        memory=1000,
        storage=500,
        volumes=[
            |volume("load", 100),
            |volume("dump", 100)
        ],
        containers=[
            |container("psql", 1000, 100, 1000, |amd64(), [|mount("dump", "/mnt/dump"), |mount("load", "/mnt/load")], "alpine", _)
        ],
        service_containers=[
            |service_container("postgres", 8000, 1000, 8000, |amd64(), [], "postgres:18", _, |wrap<StringMap>(|entry("POSTGRES_PASSWORD", "example_pwd")), _)
        ]
    )
    stopRunner[runner=runner]()

    readData: readLocal(path=example_sql_file)
    loadData: stepOnWithInput[runner=runner, logger=logger](
        name="loadData",
        in_file="data.sql",
        in_filesystem="load",
        executor_name="psql",
        commands=|raw_commands([
            "apk add postgresql18-client",
            "psql --no-psqlrc --host postgres --username postgres --file /mnt/load/data.sql postgres"
        ]),
        environment=|wrap<Environment>(|environment(|entry("PGPASSWORD", "example_pwd"), _, false, false))
    )

    dumpData: stepOn[runner=runner, logger=logger](
        name="dumpData",
        out_file="dump.data",
        out_filesystem="dump",
        executor_name="psql",
        commands=|raw_commands([
            "pg_dump --host postgres --username postgres --format=c --clean --file /mnt/dump/dump.data postgres"
        ]),
        environment=|wrap<Environment>(|environment(|entry("PGPASSWORD", "example_pwd"), _, false, false))
    )
    readLogErrors: logErrors[logger=logger](label="readData")
    readData.errors -> readLogErrors.messages

    Self.trigger -> setupRunner.trigger,ready -> readData.trigger,data -> loadData.data,success -> dumpData.trigger,data -> Self.dump
                    setupRunner.ready ----------------------------------> loadData.trigger         dumpData.finished -----> stopRunner.trigger

    dumpDataSetState: setServiceState[logger=logger](
        name="Make Data Dump",
        description="Connect to PostgreSQL and make a DB dump",
        github=github_status,
        github_project=github_project,
        github_sha=github_sha,
        github_token=github_token,
        gitlab=gitlab_status,
        gitlab_pipeline_id=gitlab_pipeline_id,
        gitlab_project_id=gitlab_project_id,
        gitlab_ref=gitlab_ref,
        gitlab_root_url=gitlab_root_url,
        gitlab_sha=gitlab_sha,
        gitlab_token=gitlab_token,
        log_response=true
    )
    Self.trigger -> dumpDataSetState.pending
    setupRunner.failed -> dumpDataSetState.canceled
    dumpData.started -> dumpDataSetState.running
    dumpData.error -> dumpDataSetState.error
    dumpData.failed -> dumpDataSetState.failed
    dumpData.success -> dumpDataSetState.success
}